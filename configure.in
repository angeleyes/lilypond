dnl configure.in   -*-shell-script-*-
dnl Process this file with autoconf to produce a configure script. 

# Bootstrap the init proces.
AC_INIT

# Bootstrap StepMake configure
# For user package:
AC_CONFIG_AUX_DIR([stepmake/bin])
# For stepmake package:
# AC_CONFIG_AUX_DIR(bin)


STEPMAKE_INIT

# List a file that identifies your package.
AC_CONFIG_SRCDIR([lily/main.cc])

# Move to aclocal.m4?
AC_CONFIG_HEADER([$CONFIGFILE.hh:config.hh.in])

# Uncomment the configuration options your package needs.


gui_b=no
AC_ARG_ENABLE(gui,
    [  --enable-gui            compile with experimental GNOME output module.  Default: off],
    [gui_b=$enableval])

# must come before any header checks
STEPMAKE_COMPILE

STEPMAKE_PYTHON(REQUIRED,2.1)
STEPMAKE_GCC(REQUIRED, 2.8)
STEPMAKE_CXX(REQUIRED)
STEPMAKE_GXX(REQUIRED, 3.0.5)
STEPMAKE_CXXTEMPLATE
STEPMAKE_LIB(REQUIRED)
# Do not use bison 1.50 and 1.75.
STEPMAKE_BISON(REQUIRED, 1.25)
STEPMAKE_FLEX(REQUIRED)
STEPMAKE_FLEXLEXER(REQUIRED)
AC_LANG_C
STEPMAKE_LOCALE
STEPMAKE_GETTEXT
STEPMAKE_MSGFMT(REQUIRED)
STEPMAKE_TEXMF(REQUIRED)
STEPMAKE_TEXMF_DIRS
STEPMAKE_GUILE_DEVEL(REQUIRED, 1.6)
STEPMAKE_KPATHSEA
STEPMAKE_MAKEINFO(REQUIRED)
STEPMAKE_PYTHON_DEVEL(REQUIRED)


AC_CHECK_HEADERS([assert.h sys/stat.h])
AC_LANG_PUSH(C++)
AC_CHECK_HEADERS([sstream])
AC_LANG_POP(C++)
AC_HEADER_STAT
AC_FUNC_MEMCMP
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([gettext isinf memmem snprintf vsnprintf gettext])

AC_MSG_CHECKING([for ec-fonts-mftraced])
ecb10=`kpsewhich ecb10.pfa 2>/dev/null`
if test -n "$ecb10"; then
    AC_MSG_RESULT([$ecb10])
else
    AC_MSG_RESULT(no)
    AC_MSG_CHECKING([for lmodern])
    lmr10=`kpsewhich lmr10.pfb 2>/dev/null`
    if test -n "$lmr10"; then
	AC_MSG_RESULT([$lmr10])
    else
	STEPMAKE_ADD_ENTRY(REQUIRED, 'package ec-fonts-mftraced >= 1.0.5')
	AC_MSG_CHECKING([for old version of ec-fonts-mftraced])
	ecrm10=`kpsewhich ecrm10.pfa 2>/dev/null`
	if test -n "$ecrm10"; then
	    AC_MSG_RESULT([$ecrm10])
	    AC_MSG_WARN([old version of ec-fonts-mftraced found, install 1.0.5 or newer])
	fi
    fi
fi

# This is developer only anyway, help pkgconfig a bit
export PKG_CONFIG_PATH
PKG_CONFIG_PATH=/usr/lib/pkgconfig:$PKG_CONFIG_PATH
PKG_CONFIG_PATH=/opt/gnome/lib/pkgconfig:$PKG_CONFIG_PATH
PKG_CONFIG_PATH=$HOME/usr/pkg/gnome/lib/pkgconfig:$PKG_CONFIG_PATH
PKG_CONFIG_PATH=$HOME/usr/pkg/pango/lib/pkgconfig:$PKG_CONFIG_PATH

STEPMAKE_FREETYPE2
STEPMAKE_PANGO

if test "$gui_b" = "yes"; then
    STEPMAKE_GTK2
fi

## Optional tools for building documentation, website, extra fonts.

# guile executable for some scripts
STEPMAKE_GUILE(OPTIONAL)
# perl for help2man.
STEPMAKE_PERL(OPTIONAL)
# mftrace for generating pfa's, pfb's
STEPMAKE_PROGS(MFTRACE, mftrace, OPTIONAL, 1.0.27)

# new makeinfo for multi-page website docs
STEPMAKE_PROGS(MAKEINFO, makeinfo, OPTIONAL, 4.7)
AC_SUBST(MAKEINFO_VERSION)


if test "$optimise_b" = yes; then
    DEFINES="$DEFINES -DSTRING_UTILS_INLINED"
    AC_DEFINE(STRINGS_UTILS_INLINED)
fi

AC_DEFINE_UNQUOTED(TOPLEVEL_VERSION, "${FULL_VERSION}")
AC_DEFINE_UNQUOTED(FLOWER_VERSION, "${FULL_FLOWER_VERSION}")

# Gather requirements and generate output.
STEPMAKE_END


test -n "$CONFIGURATION" && mc=" conf=$CONFIGURATION" || mc=""

cat <<EOF

Type:
    make$mc all       to build LilyPond
    make$mc install   to install LilyPond
    make$mc help      to see all possible targets

EOF
