% 
% feta-toevallig.mf --  implement Accidentals
% 
% source file of the Feta (Font-En-Tja) music font
% 
% (c) 1997--2002 Han-Wen Nienhuys <hanwen@cs.uu.nl>
% 


%
% Accidentals from various sources, notably
%
%   Baerenreiter edition of Schuberts `Auf dem Strom' (sharp, natural)
%   F Hofmeister edition of Muellers `Etueden fuer Horn'  (double sharp, flat)
%


%tracingall;
%proofing := 2;
%\tracingequations:= tracingonline := 1;

fet_begingroup("accidentals");

%
% The beams of most sharps have horizontal endings (as if drawn with 
% a square pen).  [Wanske] does not mention this, so we'll just ignore 
% this fact
%
fet_beginchar("Sharp" , "1", "sharp");
	set_char_box(0, 1.1 staff_space#, 1.5 staff_space#, 
		1.5 staff_space#);

	save interbeam, interstem, beamheight, beamwidth, 
		stemwidth, beamslope;

	interbeam := 1.05 staff_space;
	interstem := 7/16 ;
	beamheight := 4 stafflinethickness;
	beamwidth := w;
	stemwidth := 1.5 stafflinethickness;
	roundness := 2 blot_diameter;

	pair center;
	center := (.5 w, 0);
	
	roundness + 2 spanwidth = beamwidth;
	roundness + 2 spanheight = beamheight;

	2 horizontal_protrusion  + interstem * beamwidth + stemwidth
		= beamwidth;
	
% wanske: whole beamheight; or ca 18 degrees
%	z2 - z1 = (beamwidth - roundness, beamheight/2);
	z2 - z1 = (beamwidth - roundness, beamheight);
	z1 + z2 = 2*center;
	beamslope = (y2-y1)/(x2-x1);

	pair hspan, vspan;
	hspan = (spanwidth, beamslope * spanwidth);
	vspan = (0, spanheight);

	
	path beam;
	beam := (hspan + vspan -- -hspan 
		+  vspan -- -hspan -vspan -- hspan - vspan -- cycle )
		shifted center;

	pickup pencircle scaled roundness;
	filldraw (beam shifted (0,-interbeam/2));

	pickup pencircle scaled stemwidth;
	x3 = x4 = xpart center;

	numeric xs;
	xs := interstem* beamwidth / 2;

	(bot y3) + -xs * beamslope = -1.5 staff_space + ypart center;
	top y4  + xs * beamslope = 1.5 staff_space + ypart center;

	draw_gridline (z3-(xs,xs* beamslope),z4-(xs,xs*beamslope),stemwidth);
	addto currentpicture also currentpicture rotated 180 shifted (w,0);
	labels(1,2,3,4);


% to check that it doesn't overshoot staffline.
%
%	pickup pencircle scaled stafflinethickness;
%	draw (0, 1.5 staff_space ) ..  (1 staff_space, 1.5 staff_space);

	fet_endchar;


%
% The stems of the natural are brushed (at least, in Barenreiter SCS )
%
%

fet_beginchar( "Natural", "0", "natural")
	save height;	
	save interbeam, interstem, beamheight, beamwidth, 
	stemwidth;
	save top_stem_thick;

	beamheight# = 4.0 stafflinethickness#;
	height# = 1.5 staff_space#;
	set_char_box(0, 2/3 staff_space#, height#, height#);

	define_pixels(height);
	define_blacker_pixels(beamheight);

	% perhaps we should have a lowres fix?
	top_stem_thick = 1.9 stafflinethickness;

	interstem + stemwidth =  w;
	stemwidth = 1.3 stafflinethickness;

	z2 -z1 = (interstem, slope * interstem);
	xpart .5 [z2,z1] = xcenter ;
	xcenter = w/2;


	pickup penrazor scaled beamheight rotated 90;
	top y2 = staff_space - 3/2 stafflinethickness ;
	slope = stafflinethickness / interstem;
	
	draw z1 .. z2;
	draw (xpart z1, -y2) .. (xpart z2, -y1);
	beamtop = top y2;

	pickup pencircle scaled stemwidth;
	x3 := round (xpart z1);
	x4 := round (xpart z2);

	penpos3(top_stem_thick, 0);
	penpos5(top_stem_thick, 0);	
	penpos4(stemwidth, 0);
	penpos6(stemwidth, 0);	
	
	y3 = height;
	top y4 = beamtop;

	x5 = x4;
	x6 = x3;
	bot y6 = -beamtop;
	y5 = - height;

	fill simple_serif (z3l, z3r, -30) -- simple_serif(z6r, z6l, -90) -- cycle;
	fill simple_serif (z5l, z5r, 30) -- simple_serif(z4r, z4l, 90) -- cycle;



	penlabels(3,4,5,6);

	labels(1,2);
	fet_endchar;


%
% Dedicated to my mom.    (3/10/97)
%
% Mamma, ik hou van je; kom je alsjeblieft terug? 
%    -- HW
%

% TODO: remove crook_fatness
% TODO: document, simplify!
%
def draw_meta_flat(expr xcenter, w, crook_fatness) =
	clearxy;
	save crook_thinness;
	save top_stem_thick, bottom_stem_thick, hair, smaller_hole;
	save center;
	pair center;
	center = (xcenter, 0);

%
%  TODO: parameterize  this
%
	if w >= 0.75 staff_space:
		smaller_hole = 0.2 stafflinethickness;
	else:
		smaller_hole = 0.0 stafflinethickness;
	fi
	crook_thinness = 1.1 stafflinethickness;
	top_stem_thick = 2 stafflinethickness;
	bottom_stem_thick = 1.2 stafflinethickness;


	z1 = (0, 2 staff_space) + center;
	z2 = (0, - 1/2 staff_space)+  center;

	penpos1(top_stem_thick, 0);
	penpos2(bottom_stem_thick, 0); 

	fill simple_serif(z1r, z1l, 30) -- z2l -- z2r -- cycle;


	y3l = (staff_space - stafflinethickness)/2 + ypart center;
	z3l = whatever [z2r,z1r];

	z3r = .26 [z2r, z1r] + (smaller_hole, 0);
	z10 = whatever [z2r, z1r] + (smaller_hole , 0);

	

	z4 = (3/8 staff_space, staff_space/2) + center;
	penpos4(whatever, 53);
	y4l - y4r = 2 crook_thinness ;
%	y4l - y4r = 6/20[crook_thinness, crook_fatness] ;

	y5r = 1/4 staff_space + ypart center;
	x5l = w + xpart center;
	penpos5(crook_fatness, -175);

	z8 = (0, - staff_space/2 - stafflinethickness/2) + center;

	z9 = (x5,0);

	save ne_angle;
	ne_angle = angle(z9 - z8);

	z7 = 8/33 [z8,z9] + (1.0 smaller_hole, 0);
	z6l = 18/33 [z8,z9] + .5 crook_thinness *dir(ne_angle -90);
  
	penpos7(crook_thinness, ne_angle + 90);

	z6r = .3 (z9-z8) + z7r;

	penpos8(whatever, ne_angle + 90);
	x8r =  xpart center - bottom_stem_thick/2;
	penlabels(range 0 thru 10);

	y10 = -1/5 staff_space;

	unfill z3r .. z4r{right} .. tension .9 
		.. z6r ---
		z7r{left} 
		.. z10{z3r-z10}  -- cycle;
	fill z8r{down}
		.. tension 0.8 ..z8l{(z9-z8)}
		.. z7l
		.. z6l
		.. z5l{up}
		.. z4l{left} .. z3l -- cycle;
		
	labels(10);
enddef;

	
%
% unfortunately, 600dpi is not enough to show the brush of the stem.
%
fet_beginchar("Flat", "-1", "flat")
	set_char_box(1.2 stafflinethickness#, .8 staff_space#, .5 staff_space#, 2 staff_space#);
	draw_meta_flat(0, w, 1/3 staff_space);
	fet_endchar;


fet_beginchar("Double Flat", "-2", "flatflat")
	save left_wid, overlap, right_wid;
	left_wid = .7;
	right_wid = .8;
	overlap = .05;
	set_char_box(1.2 stafflinethickness#, (left_wid + right_wid -overlap) *staff_space#, .5 staff_space#, 2 staff_space#);
	draw_meta_flat(0, left_wid*  staff_space, 1/3 staff_space);
	draw_meta_flat((left_wid - overlap) *staff_space,  
		right_wid *staff_space, 1/3 staff_space);
	fet_endchar;

fet_beginchar("Double Sharp", "2", "sharpsharp")
	set_char_box(0, staff_space#, .5 staff_space#, .5 staff_space#);
	save klaverblad, klaversteel;

	klaversteel = 1/15 staff_space;
	klaverblad = .35 staff_space;

	z1 = (klaversteel, 0);
	z2 = (w/2 - klaverblad / 10, h - klaverblad);
	z3 = (w/2, h);
	z4 = z2 reflectedabout((0,0), (1,1));
	z5 = z1 reflectedabout((0,0), (1,1));

	labels(1,2,3,4,5);
	pickup pencircle scaled 1/20 staff_space;
	filldraw  
		z1{dir 45} .. {right}z2 -- z3 -- 
		z4{down} .. {dir 225}z5 .. cycle;
	

	addto currentpicture also currentpicture yscaled (-d/h);
	addto currentpicture also currentpicture xscaled (-1);

	% ugh
	currentpicture := currentpicture  shifted (w/2,0);



fet_endchar;


def draw_paren =
  	save leftindent;
	leftindent# := .2 staff_space#;
	define_pixels(leftindent);
  	set_char_box(0, .5 staff_space#+stafflinethickness#,
	  staff_space#, staff_space#);
	z1 = (leftindent,h);
	z2 = (w-stafflinethickness,0);
	z3 = (leftindent,-d);

	penpos1(stafflinethickness, 35);
	penpos2(2 stafflinethickness, 0);
	penpos3(stafflinethickness, -35);

	penlabels(1,2,3);
	fill z2l{down} .. simple_serif(z3l, z3r, 90) ..	z2r{up}
	.. simple_serif(z1r, z1l, 90) .. z2l{down} -- cycle;
enddef;
  
fet_beginchar("Right Parenthesis", "rightparen", "rightparen")
	draw_paren;
fet_endchar;

fet_beginchar("Left Parenthesis", "leftparen", "leftparen")
	draw_paren;
        currentpicture := currentpicture xscaled -1;
        set_char_box(charwd, charbp, chardp, charht);
fet_endchar;

fet_endgroup("accidentals");

