% bolletjes.mf
% part of LilyPond's pretty-but-neat music font

% most beautiful noteheads are pronounced, not circular, 
% and not even symmetric.
% These examples are inspired by [Wanske] [see literature list]

%noteheight#:=interline#;

% even more pronounced (almost overdone), just like the original
noteheight#:=interline#+1.5stafflinethickness#;


% setup user vars
def setup_notehead_vars =
	save a_b,err_y_a,tilt,superness;
	save ai_a,ai_bi,err_y_ai,err_x_bi,inner_tilt,inner_superness;
	save b_h,a_w;
	enddef;

% setup addititional vars and calc them
def notehead_calc =
	save a,beta,ai,bi, ht, wd;
	ht# =noteheight#;
	2beta#=ht#*b_h;
	a# = beta#*a_b;
	wd# = 2a# / a_w;
	ai# = a# * ai_a;
	bi# = ai#/ai_bi;
	define_pixels(a,beta);
	define_pixels(ai,bi);
	set_char_box(0, wd#, .5 ht#, .5 ht#);
	enddef;


% draw the outer and inner ellipse.
def notehead_draw =
	path black,white;
	black=distorted_ellipse(a,beta,a*err_y_a,0,superness);
	white=distorted_ellipse(ai,bi,ai*err_y_ai,bi*err_x_bi,inner_superness);
if test>1: %fixme
		save x;
		x1=-x3=a; x2=x4=0; y1=y3=0; y2=-y4=b;
		penlabels(1,2,3,4);
		test_grid;
else:
		black:=black rotated tilt;
		black:=black shifted (w/2,0);
		white:=white rotated inner_tilt;
		white:=white shifted (w/2,0);
fi
		fill black;
		unfill white;

		
	enddef;


fet_begingroup("balls");

% whole note
% Wanske, p.38
fet_beginchar("Whole notehead", "0", "wholeball")
	setup_notehead_vars;
	
	a_b:=1.80;
	err_y_a:=0; % no slant
	tilt:=0;
	superness:=0.707;
	ai_a:=0.508;
	% ai_bi:=1.23;
	ai_bi:=1.30; % jcn
	% err_y_ai:=0.0938;
	% err_x_bi:=0;
	err_y_ai:=0;
	err_x_bi:=0.115;
	% inner_tilt:=135;
	inner_tilt:=125; % jcn
	% inner_superness:=0.69;
	inner_superness:=0.68; % jcn
	b_h:=1; %no rotate-> no height correction
	a_w:=1; % no rotate-> no width correction

	notehead_calc;
	notehead_draw;
fet_endchar;


% half note
% Wanske, p.39
fet_beginchar("Half notehead", "1", 
	"halfball")
	setup_notehead_vars;
		% a_b:=1.49; % after text
	a_b:=1.50; % after drawing
	err_y_a:=0.157;
	tilt:=34;
	% superness:=0.66;
	superness:=0.67; % jcn
	% ai_a:=0.863;
	ai_a:=0.850; % jcn
	% ai_bi:=3.14;
	ai_bi:=3.30; % jcn
	err_y_ai:=0;
	err_x_bi:=-0.12;
	inner_tilt:=tilt;
	inner_superness:=0.80;
	b_h:=0.935;
	a_w:=1.12;

	notehead_calc;
	notehead_draw;
fet_endchar;


% quarter note
% Wanske p.38
fet_beginchar("Quart notehead", "2", "quartball")
	setup_notehead_vars;
	% a_b:=1.57; % after text
	a_b:=1.54; % after drawing
	err_y_a:=0.044;
	tilt:=32;
	superness:=0.707;
	ai_a:=0;
	ai_bi:=1;
	err_y_ai:=0;
	err_x_bi:=0;
	inner_tilt:=0;
	inner_superness:=0.707;
	b_h:=0.85;
	a_w:=1.09;

	notehead_calc;
	notehead_draw;
fet_endchar;



% from MO*gen.mf; (pass the barfbag?)
def add_mirror (expr pone, ptwo) =
  addto currentpicture also currentpicture
        reflectedabout (round(pone), round(ptwo))
enddef;

def fill_square (expr xwidth, ywidth, zshift) =
  fill unitsquare xscaled xwidth yscaled ywidth shifted zshift
enddef;

% ugh
nhh#:=interline#;
nhw#:=6/5interline#;
define_pixels(nhh,nhw);

fet_beginchar("Brevis notehead","-1","breveball"); 
	set_char_box(0, 2interline#,.5 interline#, .5interline#);
	fill_square (5/4nhw, .25nhh, (0,.25nhh));
	add_mirror (origin, right);
	x1=x2=0; x3=x4=5/4nhw; y1=-y2=y3=-y4=.7nhh;
	pickup pencircle scaled stafflinethickness;
	draw z1--z2; draw z3--z4;
 	push_picture(currentpicture);
fet_endchar;

fet_beginchar("Longa notehead","-2","longaball");
	set_char_box(0, 2 interline#,.5interline#, .5interline# );
	currentpicture := pop_picture;
	pickup pencircle scaled stafflinethickness;
	draw (5/4nhw,-.7nhh)--(5/4nhw,-1.7nhh);
fet_endchar;

% fet_endgroup("noteheads");
fet_endgroup("balls");

