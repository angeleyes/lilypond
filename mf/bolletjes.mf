% bolletjes.mf
% part of LilyPond's pretty-but-neat music font
% third try at bolletjes
% most beautiful bolletjes are not circular, and not even symmetric.

interline#:=staffsize#/(stafflines-1)+stafflinethickness#;

def test_grid =
if test>1:
	proofrulethickness 1pt#;
	makegrid(0pt,0pt for i:=-5pt step 1pt until 5pt: ,i endfor)
		(0pt,0pt for i:=-5pt step 1pt until 5pt: ,i endfor);
	proofrulethickness .1pt#;
	makegrid(0pt,0pt for i:=-4.8pt step .2pt until 4.8pt: ,i endfor)
		(0pt,0pt for i:=-4.8pt step .2pt until 4.8pt: ,i endfor);
fi
	enddef;

% a: x diameter
% b: y diameter
% err_x: drift of y axis at top
% err_y: drift of x axis at right
def distorted_ellipse(expr a,b,err_y,err_x,super) =
	superellipse((a,err_x),(-err_y,b),(-a,-err_x),(err_y,-b),super);
	enddef;

def begin_notehead =
	begingroup;
	save a_b,err_y_a,alpha,super;
	save ai_a,ai_bi,err_y_ai,err_x_bi,alphai,superi;
	save b_h,a_w;
	enddef;

def end_notehead(expr code,interline,name) =
	save a,b,h,w,ai,bi;
	h#=interline;
	2b#=h#*b_h;
	a#=b#*a_b;
	w#=2a#/a_w;
	ai#=a#*ai_a;
	bi#=ai#/ai_bi;
	define_pixels(a,b);
	define_pixels(w,h);
	define_pixels(ai,bi);
 	fet_beginchar(code,w#,h#,0,name);
		path black,white;
		black=distorted_ellipse(a,b,a*err_y_a,0,super);
		white=distorted_ellipse(ai,bi,ai*err_y_ai,bi*err_x_bi,superi);
if test>0:
		save x;
		x1=-x3=a; x2=x4=0; y1=y3=0; y2=-y4=b;
		penlabels(1,2,3,4);
		test_grid;
else:
		black:=black rotated alpha;
%		black:=black shifted (w/2,h/2);
		black:=black shifted (w/2,0);
		white:=white rotated alphai;
%		white:=white shifted (w/2,h/2);
		white:=white shifted (w/2,0);
fi
		fill black;
		unfill white;
		endchar;
		endgroup;
	enddef;
		
% whole note
% Wanske, p.38
% begin_notehead(incr code,interline#,"Whole notehead");
begin_notehead;
	a_b:=1.80;
	err_y_a:=0; % no slant
	alpha:=0;
	super:=0.707;
	ai_a:=0.508;
	ai_bi:=1.23;
	% err_y_ai:=0.0938;
	% err_x_bi:=0;
	err_y_ai:=0;
	err_x_bi:=0.115;
	alphai:=135;
	superi:=0.69;
	b_h:=1; %no rotate-> no height correction
	a_w:=1; % no rotate-> no width correction
	end_notehead(incr code,interline#,"Whole notehead");

% half note
% Wanske, p.39
begin_notehead;
	a_b:=1.49; % after text
	% a_b:=1.50; % after drawing
	err_y_a:=0.157;
	alpha:=34;
	super:=0.66;
	ai_a:=0.863;
	ai_bi:=3.14;
	err_y_ai:=0;
	err_x_bi:=-0.12;
	alphai:=alpha;
	superi:=0.80;
	b_h:=0.935;
	a_w:=1.12;
	end_notehead(incr code,interline#,"Half notehead");

% quarter note
% Wanske p.38
begin_notehead;
	a_b:=1.57; % after text
	% a_b:=1.54; % after drawing
	err_y_a:=0.044;
	alpha:=32;
	super:=0.707;
	ai_a:=0;
	ai_bi:=1;
	err_y_ai:=0;
	err_x_bi:=0;
	alphai:=0;
	superi:=0.707;
	b_h:=0.85;
	a_w:=1.09;
	end_notehead(incr code,interline#,"Quarter notehead");

end.

