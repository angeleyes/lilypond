% -%-Fundamental-%- -*-Metafont-*-
% parmesan-clefs.mf -- implement ancient clefs
% 
% source file of LilyPond's pretty-but-neat music font
%
% (c)  2001--2003 Juergen Reuter <reuter@ipd.uka.de>
%

fet_begingroup ("clefs")

%
% character aligment:
%
%   Each clef is associated with a particular pitch: the treble clef
%   with the 'g', the alto clef with the 'c', the bass clef with the
%   'f', etc.  The shape of each clef character defines a vertical
%   position that is assumed to represent this pitch.  For the treble
%   clef, it is the vertical position of the center of the spiral
%   ending that represents the 'g' pitch.  For the bass clef, it is
%   the center between the two fat dots that define the vertical
%   position of the 'f' pitch.  For the alto clef, it is the vertical
%   center of the clef that is aligned with the 'c' pitch.  For each
%   clef character, this center should be vertically aligned with the
%   point (0, 0).  The horizontal alignment of each clef character
%   should be such that the vertical line through the point (0, 0)
%   touches the left-most edge of the clef.
%
%   TODO: document exact_center
%
% set_char_box() conventions:
%
% * breapth: Ignored (as far as I know).  Should be set to 0.
%
% * width: Should match the head's width.
%
% * depth: Should match the bottom edge of the head.  Affects vertical
%   collision handling.
%
% * height: Should match the top edge of the head.  Affects vertical
%   collision handling.
%


%%%%%%%%
%
%
%
% Editio Vaticana
%
%
%
def draw_vaticana_do_clef(expr exact_center, reduction) = 
	save reduced_il;

	reduced_il# = staff_space# * reduction;
	
	set_char_box(0 - xpart exact_center,
		     0.5reduced_il# + xpart exact_center,
		     0.8reduced_il# - ypart exact_center,
		     0.8reduced_il# + ypart exact_center);

	define_pixels(reduced_il);

	pickup pencircle xscaled 0.6linethickness yscaled 0.6 reduced_il;

	save za, zb, zc, zd, ze, zf;
	pair za, zb, zc, zd, ze, zf;

	rt  za = exact_center + (0.50reduced_il, -.45reduced_il);
	    zb = exact_center + (0.25reduced_il, -.50reduced_il);
	lft zc = exact_center + (0.00reduced_il, -.25reduced_il);
	lft zd = exact_center + (0.00reduced_il, +.25reduced_il);
	    ze = exact_center + (0.25reduced_il, +.50reduced_il);
	rt  zf = exact_center + (0.50reduced_il, +.45reduced_il);
	draw za .. zb .. zc --	% lower punctum
	     zd .. ze .. zf;	% upper punctum
enddef;


fet_beginchar("Ed. Vat. do clef", "vaticana_do", "vatdoclef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_vaticana_do_clef((0,0), 1.0);
fet_endchar;
fet_beginchar("Ed. Vat. do clef", "vaticana_do_change", "vatcdoclef")
	draw_vaticana_do_clef((0,0), 1.0); % no reduction
fet_endchar;


def draw_vaticana_fa_clef(expr exact_center, reduction) = 

	save reduced_il, z;
	reduced_il# = staff_space# * reduction;
	define_pixels(reduced_il);

	save za, zb, zc, zd, ze;
	pair za, zb, zc, zd, ze;

	%left-handed punctum
	pickup pencircle xscaled 0.6linethickness yscaled 0.5reduced_il;
	lft za = exact_center + (+0.00reduced_il, +0.00reduced_il);
	    zb = exact_center + (+0.25reduced_il, +0.05reduced_il);
	rt  zc = exact_center + (+0.50reduced_il, -0.05reduced_il);
	draw za .. zb .. zc;

	%stem
	pickup pencircle scaled 0.6linethickness;
	xpart zc = xpart zd = xpart ze;
	ypart zd = ypart exact_center = bot ypart ze + 1.5reduced_il;
	draw zd -- ze;

	%right-handed puncta as in do clef
	draw_vaticana_do_clef(exact_center + (0.55reduced_il#, 0), reduction);

	set_char_box(0 - xpart exact_center,
		     1.05reduced_il# + xpart exact_center,
		     1.5reduced_il# - ypart exact_center,
		     0.8reduced_il# + ypart exact_center);
enddef;


fet_beginchar("Ed. Vat. fa clef", "vaticana_fa", "vatfaclef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_vaticana_fa_clef((0,0), 1.0);
fet_endchar;
fet_beginchar("Ed. Vat. fa clef", "vaticana_fa_change", "vatcfaclef")
	draw_vaticana_fa_clef((0,0), 1.0); % no reduction
fet_endchar;

%%%%%%%%
%
%
%
% Editio Medicaea
%
%
%
def draw_medicaea_do_clef(expr exact_center, reduction) = 
	save reduced_il, reduced_slt;
	reduced_il# = staff_space# * reduction;
	reduced_slt# = linethickness# * reduction;
	define_pixels(reduced_il);
	define_pixels(reduced_slt);

	save flag_height;
	flag_height# = 0.5 reduced_il#;
	define_pixels(flag_height);

	%upper flag
	save za, zb;
	pair za, zb;
	pickup pencircle xscaled reduced_slt yscaled flag_height;
	xpart exact_center
		= lft xpart za
		= rt xpart zb - reduced_il;
	ypart za = ypart exact_center +
		0.5 (reduced_il - flag_height - staff_space);
	ypart zb = ypart za - reduced_il + flag_height;
	draw za -- zb;

	%lower flag
	save za, zb;
	pair za, zb;
	pickup pencircle xscaled reduced_slt yscaled flag_height;
	xpart exact_center
		= lft xpart za
		= rt xpart zb - reduced_il;
	ypart za = ypart exact_center +
		 0.5 (reduced_il - flag_height + staff_space);
	ypart zb = ypart za - reduced_il + flag_height;
	draw za -- zb;

	%stem
	save za, zb;
	pair za, zb;
	pickup pencircle scaled reduced_slt;
	lft xpart za = lft xpart zb = xpart exact_center;
	ypart exact_center
		= top ypart zb - 1.5 reduced_il
		= bot ypart za + 1.5 reduced_il;
	draw za -- zb;

	set_char_box(0 - xpart exact_center,
		     1.0reduced_il# + xpart exact_center,
		     1.5reduced_il# - ypart exact_center,
		     1.5reduced_il# + ypart exact_center);
enddef;


fet_beginchar("Ed. Med. do clef", "medicaea_do", "meddoclef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_medicaea_do_clef((0,0), 1.0);
fet_endchar;
fet_beginchar("Ed. Med. do clef", "medicaea_do_change", "cmeddoclef")
	draw_medicaea_do_clef((0,0), .8);
fet_endchar;


def draw_medicaea_fa_clef(expr exact_center, reduction) = 
	% inspired by Regensburger Edition of Medicaea (1885/86), in:
	% MGG, volume 2, col. 1327 ("Choralreform"), fig. 2.

	save reduced_il, reduced_slt;
	reduced_il# = staff_space# * reduction;
	reduced_slt# = linethickness# * reduction;
	define_pixels(reduced_il);
	define_pixels(reduced_slt);

	save za, zb, zc, zd, ze;
	pair za, zb, zc, zd, ze;

	%stem
	pickup pencircle scaled linethickness;
	xpart za = xpart zb =
		xpart exact_center + 0.4reduced_il;
	ypart za = ypart exact_center = bot ypart zb + 1.5reduced_il;
	draw za -- zb;

	%left-handed punctum
	pickup pencircle xscaled reduced_slt yscaled reduced_il;
	lft zc = exact_center;
	zd = exact_center + (0.4reduced_il, 0);
	draw zc -- zd;

	%right-handed puncta as in do clef
	draw_medicaea_do_clef(exact_center + (0.7reduced_il#, 0), reduction);

	set_char_box(0 - xpart exact_center,
		     1.7reduced_il# + xpart exact_center,
		     1.5reduced_il# - ypart exact_center,
		     1.5reduced_il# + ypart exact_center);
enddef;


fet_beginchar("Ed. Med. fa clef", "medicaea_fa", "medfaclef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_medicaea_fa_clef((0,0), 1.0);
fet_endchar;
fet_beginchar("Ed. Med. fa clef", "medicaea_fa_change", "cmedfaclef")
	draw_medicaea_fa_clef((0,0), .8);
fet_endchar;


%%%%%%%%
%
%
%
% Mensural Notation
%
%
%

%
% width:        interval from left end to right end
% height:       interval from bottom of lower beam to top of upper beam
% exact_center: the coordinates of the vertical center point of the
%               left edge.
%
def draw_brevis(expr exact_center, bwidth, bheight, blinethickness) =

	save xoffs, yoffs, brevis_width, brevis_height, linethickness;
	xoffs# = xpart exact_center;
	yoffs# = ypart exact_center;
	brevis_width# = bwidth; brevis_height# = bheight;
	linethickness# = blinethickness;

	save beam_width, beam_height, serif_size, serif_protrude, hole_height;
	beam_width# = 1.4 linethickness#;
	hole_height# = 3 linethickness#;
	2 beam_height# + hole_height# = brevis_height#;
	serif_size# = (hole_height# - linethickness#)/2;
	serif_protrude# = 1.5 serif_size#;

	define_pixels(xoffs, yoffs, brevis_width, brevis_height, linethickness);
	define_pixels(beam_width, beam_height, serif_size, serif_protrude);

	penpos1(beam_width, 0);
	penpos2(beam_width, 0);
	penpos3(beam_height, 90);
	penpos4(beam_height, 90);
	penpos5(beam_width, 180);
	z1l = (xoffs, yoffs);
	z2l = z1l + (0, -linethickness);
	z3r = z2r + serif_size*(1,-1);
	y4r = y3r;
	x4l = x1l + brevis_width/2;
	z5l = z3l + (-serif_size, -serif_protrude);
	fill z1r -- z1l -- z5r{down} .. z5l{up} .. z3l{right}
		-- z4l -- z4r -- z3r{left} .. z2r{up} -- cycle;

	addto currentpicture also currentpicture
		yscaled -1 shifted (0, 2*yoffs);

	pickup pencircle scaled linethickness;
	top y6 = yoffs + brevis_height/2;
	bot y7 = yoffs - brevis_height/2;
	lft x6 = lft x7 = xoffs;
	draw z6 -- z7;

	addto currentpicture also currentpicture
		xscaled -1 shifted (2xoffs + brevis_width, 0);
enddef;


def draw_neo_mensural_c_clef(expr exact_center, reduction) = 
	save reduced_il, reduced_slt, stem_width;
	reduced_il# = staff_space# * reduction;
	reduced_slt# = linethickness# * reduction;
	stem_width# = 1.4 reduced_slt#;
	define_pixels(reduced_il, reduced_slt, stem_width);

	draw_brevis(exact_center + (3reduced_slt#, 0),
		    2reduced_il#, reduced_il#, reduced_slt#);

	save xoffs, yoffs;
	xoffs# = xpart exact_center;
	yoffs# = ypart exact_center;
	define_pixels(xoffs, yoffs);

	pickup pencircle xscaled stem_width yscaled blot_diameter;
	lft x8  = lft x9  = xoffs;
	lft x10 = lft x11 = lft x8  + 3reduced_slt;
	rt  x12 = rt  x13 = lft x10 + 2reduced_il;
	rt  x14 = rt  x15 = rt  x12 + 3reduced_slt;
	top y9 - bot y8 = 4reduced_il;
	top y9 + bot y8 = 2yoffs;
	y12 = y14 = y10 = y8; y13 = y15 = y11 = y9;
	draw z8 -- z9;
	draw z10 -- z11;
	draw z12 -- z13;
	draw z14 -- z15;

	set_char_box(0 - xpart exact_center,
		     2reduced_il# + 6reduced_slt# + xpart exact_center,
		     2reduced_il# - ypart exact_center,
		     2reduced_il# + ypart exact_center);
enddef;


fet_beginchar("neo-mensural c clef", "neo_mensural_c", "neomenscclef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_neo_mensural_c_clef((0,0), 1.0);
fet_endchar;
fet_beginchar("neo-mensural c clef", "neo_mensural_c_change", "cneomenscclef")
	draw_neo_mensural_c_clef((0,0), .8);
fet_endchar;


def draw_petrucci_c_clef(expr exact_center, flare_align, reduction) = 
	% inspired by Josquin Desprez, "Stabat Mater", Libro tertio,
	% 1519, printed by Petrucci, in: MGG, volume 7, Table 11.
	% Also by Petrucci's Canti C, Venedig 1503.  In: MGG, volume
	% 9, p. 1681/1682.

	save reduced_il, reduced_slt;
	reduced_il# = staff_space# * reduction;
	reduced_slt# = linethickness# * reduction;
	define_pixels(reduced_il);

	draw_brevis(exact_center + (0, 0.5staff_space#),
		    reduced_il#, reduced_il#, reduced_slt#);

	addto currentpicture also currentpicture shifted (0, -staff_space);

	save half_reduced_il, left_depth, left_height;
	half_reduced_il# = staff_space# * sqrt(reduction);
	left_height# = half_reduced_il# * min(3.2, 3.2 + 0.2 + flare_align);
	left_depth# = half_reduced_il# * min(3.2, 3.2 + 0.2 - flare_align);

	define_pixels(half_reduced_il);
	define_pixels(left_depth, left_height);

	save xoffs, yoffs;
	xoffs# = xpart exact_center;
	yoffs# = ypart exact_center;
	define_pixels(xoffs, yoffs);

	pickup pencircle xscaled 1.4 linethickness yscaled blot_diameter;
	lft x8 = lft x9 = xoffs;
	top y8 = yoffs + left_height;
	bot y9 = yoffs - left_depth;
	draw z8 .. z9;

	rt x10 = rt x11 = xoffs + brevis_width;
	y10 = min(y8 - 0.2*half_reduced_il, yoffs + 2.2half_reduced_il);
	y11 = max(y9 + 0.2*half_reduced_il, yoffs - 2.2half_reduced_il);
	draw z10 .. z11;

	set_char_box(0 - xpart exact_center,
		     reduced_il# + xpart exact_center,
		     left_depth# - ypart exact_center,
		     left_height# + ypart exact_center);
enddef;


fet_beginchar("petrucci c1 clef", "petrucci_c1", "petruccic1clef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_petrucci_c_clef((0,0), +2, 1.0);
fet_endchar;
fet_beginchar("petrucci c1 clef", "petrucci_c1_change", "cpetruccic1clef")
	draw_petrucci_c_clef((0,0), +2, .8);
fet_endchar;

fet_beginchar("petrucci c2 clef", "petrucci_c2", "petruccic2clef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_petrucci_c_clef((0,0), +1, 1.0);
fet_endchar;
fet_beginchar("petrucci c2 clef", "petrucci_c2_change", "cpetruccic2clef")
	draw_petrucci_c_clef((0,0), +1, .8);
fet_endchar;

fet_beginchar("petrucci c3 clef", "petrucci_c3", "petruccic3clef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_petrucci_c_clef((0,0), 0, 1.0);
fet_endchar;
fet_beginchar("petrucci c3 clef", "petrucci_c3_change", "cpetruccic3clef")
	draw_petrucci_c_clef((0,0), 0, .8);
fet_endchar;

fet_beginchar("petrucci c4 clef", "petrucci_c4", "petruccic4clef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_petrucci_c_clef((0,0), -1, 1.0);
fet_endchar;
fet_beginchar("petrucci c4 clef", "petrucci_c4_change", "cpetruccic4clef")
	draw_petrucci_c_clef((0,0), -1, .8);
fet_endchar;

fet_beginchar("petrucci c5 clef", "petrucci_c5", "petruccic5clef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_petrucci_c_clef((0,0), -2, 1.0);
fet_endchar;
fet_beginchar("petrucci c5 clef", "petrucci_c5_change", "cpetruc5iceclef")
	draw_petrucci_c_clef((0,0), -2, .8);
fet_endchar;


def draw_mensural_c_clef(expr exact_center, reduction) =
	% inspired by Ockeghem, "Missa Prolationum", in: MGG, volume
	% 9, table 94.

	save reduced_il;
	reduced_il# = staff_space# * reduction;
	draw_brevis(exact_center + (0, 0.5staff_space#),
		    2reduced_il#, 0.8staff_space#, 0.8linethickness#);
	define_pixels(reduced_il);
	addto currentpicture also currentpicture shifted (0, -staff_space);
	addto currentpicture also currentpicture shifted (0, -staff_space);

	save half_reduced_il;
	half_reduced_il# = staff_space# * sqrt(reduction);
	define_pixels(half_reduced_il);

	save xoffs, yoffs;
	xoffs# = xpart exact_center;
	yoffs# = ypart exact_center;
	define_pixels(xoffs, yoffs);

	pickup pencircle xscaled 1.4 linethickness yscaled blot_diameter;
	lft x8 = lft x9 = xoffs;
	top y8 = yoffs + 2.2 half_reduced_il;
	bot y9 = yoffs - 2.2 half_reduced_il - staff_space;
	draw z8 .. z9;

	rt x10 = rt x11 = xoffs + brevis_width;
	y10 = yoffs + 1.4half_reduced_il;
	y11 = yoffs - 1.4half_reduced_il - staff_space;
	draw z10 .. z11;

	set_char_box(0 - xpart exact_center,
		     2reduced_il# + xpart exact_center,
		     -yoffs# + 2.2 half_reduced_il# + staff_space# - ypart exact_center,
		     yoffs# + 2.2 half_reduced_il# + ypart exact_center);
enddef;


fet_beginchar("mensural c clef", "mensural_c", "menscclef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_mensural_c_clef((0,0), 1.0);
fet_endchar;
fet_beginchar("mensural c clef", "mensural_c_change", "cmenscclef")
	draw_mensural_c_clef((0,0), .8);
fet_endchar;

def draw_diamond(expr exact_center, reduction) =
	save stem_width, reduced_il, reduced_nht, holeheight, beamheight;
	save rh_height, rh_width;

	stem_width# = 1.4 reduced_slt#;
	reduced_il# = staff_space# * reduction;
	reduced_nht# = noteheight# * reduction;
	holeheight# = 3 reduced_slt#;
	beamheight# = 0.4(reduced_nht# - holeheight#);

	rh_height# = 1.2reduced_il#;
	rh_width# / rh_height# = tand(30);

	define_pixels(beamheight);
	define_pixels(stem_width);
	define_pixels(rh_height);
	define_pixels(rh_width);

	pickup pencircle
		xscaled beamheight
		yscaled stem_width
		rotated 45;

	draw
		exact_center + (-rh_width/2, 0) --
		exact_center + (0, rh_height/2) --
		exact_center + (+rh_width/2, 0) --
		exact_center + (0, -rh_height/2) --
		cycle;
enddef;

def draw_petrucci_f_clef(expr exact_center, reduction) =
	% inspired by L'homme arme super voces musicales in Misse
	% Josquin, 1502, Petrucci, in: MGG, volume 7, col. 200; also
	% inspired by Gaspar van Weerbeke, "Virgo Maria" (1502), in:
	% MGG, volume 9, col. 653 ("Motette"), fig. 3.; also by Andr'e
	% Campra, "Entr'ee des s'er'enades" (1710), in: MGG, volume 2,
	% col. 1649 ("Contredanse"), fig. 2.
	%

	save reduced_il, reduced_slt;
	reduced_il# = staff_space# * reduction;
	reduced_slt# = linethickness# * reduction;
	define_pixels(reduced_il);
	define_pixels(reduced_slt);

	draw_brevis(exact_center, reduced_il#, reduced_il#, reduced_slt#);

	save stem_width, interline;
	stem_width# = 1.4 reduced_slt#;
	interline# = staff_space#;
	define_pixels(stem_width);
	define_pixels(interline);

	addto currentpicture also currentpicture
		yscaled -1 shifted (0, 2*(ypart exact_center));
	addto currentpicture also currentpicture 
		xscaled -1 shifted (2x4l,0);


	% brevis stem
	pickup pencircle xscaled stem_width yscaled blot_diameter;
	rt z8 = exact_center + (reduced_il, 0);
	z9 = z8 + (0, -4reduced_il);
	draw z8 .. z9;

	% upper diamond
	draw_diamond(exact_center + (1.6interline*reduction, interline/2),
		     reduction);
	pickup pencircle xscaled stem_width yscaled blot_diameter;
	z10 = exact_center +
	     (1.6interline*reduction + stem_width/2, interline*reduction);
	top z11 = z10 + (0, 1.5interline*reduction);
	draw z10 .. z11; % diamond stem

	% lower diamond
	draw_diamond(exact_center + (1.6interline*reduction, -interline/2),
		     reduction);
	pickup pencircle xscaled stem_width yscaled blot_diameter;
	z12 = exact_center +
	      (1.6interline*reduction - stem_width/2, -interline*reduction);
	bot z13 = z12 + (0, -3.5interline*reduction);
	draw z12 .. z13; % diamond stem

	save reduced_il, rh_height, rh_width;
	reduced_il# = staff_space# * reduction;
	rh_height# = 1.2reduced_il#;
	rh_width# / rh_height# = tand(30);
	set_char_box(0 - xpart exact_center,
		     1.6interline#*reduction + 0.5rh_width# + xpart exact_center,
		     4.5*interline#*reduction - ypart exact_center,
		     2.5*interline#*reduction + ypart exact_center);
enddef;


fet_beginchar("petrucci f clef", "petrucci_f", "petruccifclef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_petrucci_f_clef((0,0), 1.0);
fet_endchar;
fet_beginchar("petrucci f clef", "petrucci_f_change", "cpetruccifclef")
	draw_petrucci_f_clef((0,0), .8);
fet_endchar;


def draw_mensural_f_clef(expr exact_center, reduction) =
	%
	% inspired by Philippe le Duc, "Dite Signori" (1590), in: MGG,
	% volume 3, col. 848 ("Duc"); also by John Dowland, "The First
	% Booke of Songes" (1597), in: MGG, volume 3, col. 721
	% ("Dowland"), fig. 3.

	save width, reduced_slt, stem_width, dot_diameter;

	width# = 1.2staff_space# * reduction;
	reduced_slt# = linethickness# * reduction;
	stem_width# = 1.4 reduced_slt#;
	dot_diameter# = 0.1 reduction * staff_space#;
	define_pixels(width, stem_width, staff_space, dot_diameter);

	pickup pencircle
		xscaled 0.2width
		yscaled stem_width
		rotated 45;

	% half circle
	lft z5 = (0, 0);
	draw	halfcircle scaled width rotated -90
		shifted (z5-exact_center);


	% upper dot
	rt x2 = xpart exact_center + width;
	top y1 = ypart exact_center + 0.5width;
	z2 - z1 = (dot_diameter, -dot_diameter);
	draw z1 -- z2;

	% lower dot
	x3 = x1;
	top y1 - bot y4 = width;
	z4 - z3 = (dot_diameter, -dot_diameter);
	draw z3 -- z4;

	set_char_box(0 - xpart exact_center,
		     width# + xpart exact_center,
		     0.5width# - ypart exact_center,
		     0.5width# + ypart exact_center);
enddef;

fet_beginchar("mensural f clef", "mensural_f", "mensfclef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_mensural_f_clef((0,0), 1.0);
fet_endchar;
fet_beginchar("mensural f clef", "mensural_f_change", "cmensfclef")
	draw_mensural_f_clef((0,0), .8);
fet_endchar;


def draw_mensural_g_clef(expr exact_center, reduction) =
	%
	% TODO: Rewrite me!  This clef looks completely awful! --jr
	%

	%
	% inspired by Francisco Guerrero, "Lib. 1.  Missarum" (1566),
	% in: MGG, volume 3, col. 858 ("Ducis"); also by Stefano
	% Fabri, "Quam speciosa veteranis" (1611), in: MGG, volume 3,
	% col. 1698 ("Fabri"); also by Philippus Dulichius,
	% "Fasciculus novus ..." (1598), in: MGG, volume 3, col. 919
	% ("Dulichius"), fig. 1; also by Noe Faignient, "Ic sal de
	% Heer myn God gebenedye" (1568), in: MGG, volume 3, col. 1735
	% ("Faignient").
	%
	% Metafont code partially inspired by Schwabacher 'G' of yswab
	% font.

	save reduced_il, stem_width, height, width, apex_o, hair;

	reduced_il#=staff_space#*reduction;

	set_char_box(0 - xpart exact_center,
		     1.2reduced_il# + xpart exact_center,
		     0.8reduced_il# - ypart exact_center,
		     1.5reduced_il# + ypart exact_center);

	stem_width# = 0.17 reduced_il#;
	height# = 1.5 reduced_il#;
	width# = 1.13 reduced_il#;
	apex_o# = 0.02 reduced_il#;
	hair# =  3 linethickness#;
	define_pixels(reduced_il, stem_width, height, width, apex_o, hair);

	penpos1(2 stem_width, -142);
	z1l = (0.715 width, 0.742 height);
	penpos1'(hair, -90);
	z1'l = z1r;
	penpos2(1.179 stem_width, -142);
	z2l = (width, 0.466 height);
	penpos3(hair, 77);
	z3 = (0.764 width, 0.067 height); 
	z4 = (0.59 width, -apex_o);
	penpos5(1.179 stem_width, 32);
	z5l=(0, 0.457 height);
	penpos6(hair, -56.5);
	z7 = (x4 - 0.843 stem_width, height + apex_o);
	z7 - z6l = whatever * dir33.5;
	penpos8(1.286 stem_width, -130);
	z8r = (0.715 width, 0.742 height) + (-apex_o, apex_o);
	z6r - z8r = whatever * (z7 - z8l);
	filldraw
		z1'r{dir45} .. z2r{down} .. z3r{dir207} .. z5r{up} ..
		z6r{z7-z6l} & z6r -- z8r -- z8l -- z7 --- z6l ...
		z5l{down} .. z4{right} .. z3l{dir27} .. z2l{up} ..
		z1l{1/3[z6l,z7]-z1l} & z1l -- z1r -- z1'r & cycle
		shifted (-exact_center + (0, -0.75reduced_il));

	penpos9(stem_width, 0);
	x9r = x4; y9 = 0.3 height;

	pickup pencircle
		scaled stem_width
		rotated 45;
	draw	z9 -- (z9 + (0, -0.4reduced_il))
		shifted (-exact_center + (0, -0.75reduced_il));

	pickup pencircle
		xscaled stem_width
		yscaled hair
		rotated 30;

	draw halfcircle
		scaled 0.5 reduced_il
		rotated -90
		shifted z8
		shifted (0, 0.25reduced_il)
		shifted (-exact_center + (0, -0.75reduced_il));

	draw halfcircle
		scaled 0.4 reduced_il
		rotated 90
		shifted (z8 + (0, 0.45 reduced_il))
		shifted (0, 0.25reduced_il)
		shifted (-exact_center + (0, -0.75reduced_il));
enddef;


fet_beginchar("mensural g clef", "mensural_g", "mensgclef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_mensural_g_clef((0,0), 1.0);
fet_endchar;
fet_beginchar("mensural g clef", "mensural_g_change", "cmensgclef")
	draw_mensural_g_clef((0,0), .8);
fet_endchar;



def draw_petrucci_g_clef(expr exact_center, reduction) =
	% inspired by Josquin Desprez, "Stabat Mater", Libro tertio,
	% 1519, printed by Petrucci, in: MGG, volume 7, Table 11.

	save reduced_il, reduced_slt;

	reduced_il# = staff_space# * reduction;
	reduced_slt# = linethickness# * reduction;
	define_pixels(reduced_il, reduced_slt);

	set_char_box(0 - xpart exact_center,
		     1.25 reduced_il# + xpart exact_center,
		     0.65 reduced_il# - ypart exact_center,
		     3.80 reduced_il# + ypart exact_center);

	save za, zb, zc, zd, ze, zf, zg, zh, zi, zj;
	pair za, zb, zc, zd, ze, zf, zg, zh, zi, zj;

	pickup pencircle
		xscaled 0.50 reduced_slt
		yscaled 0.22 reduced_il
		rotated -35;

	lft za = exact_center + (0.80 reduced_il, +0.00 reduced_il);
	lft zb = exact_center + (1.00 reduced_il, +1.20 reduced_il);
	lft zc = exact_center + (0.70 reduced_il, +2.00 reduced_il);
	lft zd = exact_center + (0.30 reduced_il, +3.00 reduced_il);
	lft ze = exact_center + (0.80 reduced_il, +3.70 reduced_il);
	lft zf = exact_center + (1.00 reduced_il, +3.00 reduced_il);
	lft zg = exact_center + (0.60 reduced_il, +2.00 reduced_il);
	lft zh = exact_center + (0.30 reduced_il, +1.70 reduced_il);
	lft zi = exact_center + (0.00 reduced_il, +0.75 reduced_il);
	lft zj = exact_center + (0.20 reduced_il, +0.60 reduced_il);

	draw za{-1,2} .. zb .. zc .. zd .. ze .. zf .. zg .. zh .. zi .. zj;

	save za, zb, zc, zd, ze, zf;
	pair za, zb, zc, zd, ze, zf;

	pickup pencircle
		xscaled 0.75 reduced_slt
		yscaled 0.33 reduced_il
		rotated -35;

	lft za = exact_center + (1.05 reduced_il, +0.45 reduced_il);
	lft zb = exact_center + (0.55 reduced_il, +0.45 reduced_il);
	lft zc = exact_center + (0.55 reduced_il, -0.45 reduced_il);
	lft zd = exact_center + (1.05 reduced_il, -0.45 reduced_il);
	lft ze = exact_center + (1.10 reduced_il, +0.00 reduced_il);
	lft zf = exact_center + (0.80 reduced_il, +0.00 reduced_il);

	draw za .. zb .. zc .. zd .. {up}ze -- zf;
enddef;


fet_beginchar("petrucci g clef", "petrucci_g", "petruccigclef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_petrucci_g_clef((0,0), 1.0);
fet_endchar;
fet_beginchar("petrucci g clef", "petrucci_g_change", "cpetruccigclef")
	draw_petrucci_g_clef((0,0), .8);
fet_endchar;



%%%%%%%%
%
%
%
% Hufnagel
%
%
%
def draw_hufnagel_do_clef(expr exact_center, reduction) =
	%
	% inspired by Graduale of Friedrich Zollner (1442), in: MGG,
	% volume 9, col. 1413 ("Neustift"), fig. 1.
	%
	save reduced_il;

	reduced_il# = staff_space# * reduction;

	define_pixels(reduced_il);

	pickup pencircle
		xscaled (0.60reduced_il)
		yscaled (0.10reduced_il)
		rotated 40;
	save za, zb, zc, zd, ze, zf;
	pair za, zb, zc, zd, ze, zf;

	za = exact_center + (0.90reduced_il, +.45reduced_il);
	zb = exact_center + (0.80reduced_il, +.45reduced_il);
	zc = exact_center + (0.50reduced_il, +.60reduced_il);
	zd = exact_center + (0.20reduced_il, +.45reduced_il);
	ze = exact_center + (0.20reduced_il, -.45reduced_il);
	zf = exact_center + (0.40reduced_il, -.55reduced_il);
	draw za .. zb .. zc -- zd -- ze -- zf;

	set_char_box(0 - xpart exact_center,
		     1.10reduced_il# + xpart exact_center,
		     0.70reduced_il# - ypart exact_center,
		     0.75reduced_il# + ypart exact_center);
enddef;


fet_beginchar("Hufnagel do clef", "hufnagel_do", "hufnageldoclef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_hufnagel_do_clef((0,0), 1.0);
fet_endchar;
fet_beginchar("Hufnagel do clef", "hufnagel_do_change", "chufnageldoclef")
	draw_hufnagel_do_clef((0,0), .8);
fet_endchar;


def draw_hufnagel_fa_clef(expr exact_center, reduction) =
	%
	% inspired by Bamberger Manuscript (15th century), in:
	% MGG, volume 2, table 59.
	%
	save reduced_il;

	reduced_il# = staff_space# * reduction;

	define_pixels(reduced_il);

	pickup pencircle
		xscaled (0.60reduced_il)
		yscaled (0.10reduced_il)
		rotated 40;
	save za, zb, zc, zd, ze, zf;
	pair za, zb, zc, zd, ze, zf;

	za = exact_center + (0.90reduced_il, +0.70reduced_il);
	zb = exact_center + (0.80reduced_il, +0.70reduced_il);
	zc = exact_center + (0.50reduced_il, +0.85reduced_il);
	zd = exact_center + (0.20reduced_il, +0.70reduced_il);
	ze = exact_center + (0.20reduced_il, -1.10reduced_il);
	draw za .. zb .. zc -- zd -- ze;

	save zg, zh, zi, zj;
	pair zg, zh, zi, zj;

	zg = exact_center + (0.90reduced_il, -0.05reduced_il);
	zh = exact_center + (0.80reduced_il, -0.05reduced_il);
	zi = exact_center + (0.50reduced_il, +0.10reduced_il);
	zj = exact_center + (0.20reduced_il, -0.05reduced_il);
	draw zg .. zh .. zi -- zj;

	set_char_box(0 - xpart exact_center,
		     1.20reduced_il# + xpart exact_center,
		     1.15reduced_il# - ypart exact_center,
		     1.00reduced_il# + ypart exact_center);
enddef;


fet_beginchar("Hufnagel fa clef", "hufnagel_fa", "hufnagelfaclef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_hufnagel_fa_clef((0,0), 1.0);
fet_endchar;
fet_beginchar("Hufnagel fa clef", "hufnagel_fa_change", "chufnagelfaclef")
	draw_hufnagel_fa_clef((0,0), .8);
fet_endchar;


def draw_hufnagel_do_fa_clef(expr exact_center, reduction) =
	draw_hufnagel_do_clef(exact_center, reduction);
	draw_hufnagel_fa_clef(exact_center + (0, -2staff_space#), reduction);
	set_char_box(0 - xpart exact_center,
		     1.20reduced_il# + xpart exact_center,
		     1.15reduced_il# + 2staff_space# - ypart exact_center,
		     0.75reduced_il# + ypart exact_center);
enddef;


fet_beginchar("Hufnagel do/fa clef", "hufnagel_do_fa", "hufnageldofaclef")
	if test = 1:
		draw_staff(-1,3, 0.0);
	fi;
	draw_hufnagel_do_fa_clef((0,0), 1.0);
fet_endchar;
fet_beginchar("Hufnagel do/fa clef", "hufnagel_do_fa_change",
	      "chufnageldofaclef")
	draw_hufnagel_do_fa_clef((0,0), .8);
fet_endchar;


fet_endgroup ("clefs")
