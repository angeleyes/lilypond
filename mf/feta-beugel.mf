% 
% feta-beugel.mf -- [Staff] braces
% 
% source file of the Feta (not an acronym for Font-En-Tja)
% pretty-but-neat music font
% 
% (c) 1997--2004 Han-Wen Nienhuys <hanwen@cs.uu.nl>
%                Jan Nieuwenhuizen <janneke@gnu.org>

input feta-autometric;
input feta-macros;

staffsize# := 20 pt#;   %% arbitrary.

input feta-params;


def abc_encode_int (expr i) =
	if i > 0:
		abc_encode_int (i div 26) & char (65 + i mod 26)
	else:
		"A"
	fi
enddef ;

% we must let the design increase for each
% font to make sure that mftrace doesn't jack up the resolution too highly
% for the longer braces.
fet_beginfont ("feta-braces-" & char (97 + font_count), (font_count + 1) * 15,
               "fetaBraces");

mode_setup;



save code;
code := 64;


def draw_brace (expr height_sharp, width_sharp, slt_sharp) =

	save pendir, height, width, thin, thick, slt;
	save penangle;
	height# := height_sharp;
	width# := width_sharp;
	slt# := slt_sharp;

%% +1  is needed because fet_beginchar increments after dumping the strings.
fet_beginchar ("brace number " & (decimal (code + 1)),
  "brace" & abc_encode_int (code - 64),
  "brace" & abc_encode_int (code - 64))

	set_char_box (0, width#, height#/2, height#/2);
	  
	define_pixels (height, width, slt);
	thin = 2 slt;
	thick = .5 width;
	
	z2 = .5 [z1, z3];
	y3l = y1 + height/2;
	x3 = x1 + width;
	y1 =0;
	x3 = - width / 2;

	pair pendir;
	pendir = unitvector(x3 - x1, y3l/6 - y1);
	penangle = angle pendir - 90;
	penpos3(thin, penangle);
	penpos2(thick, angle(z3 -z1) -90 );
	penpos1(2/3 thin, penangle);
	
	penlabels(1,2,3);
	fill z2r ..  simple_serif (z3r, z3l, 90)
		.. z2l .. simple_serif (z1l, z1r, 90) ..cycle;

	addto currentpicture also currentpicture yscaled -1;
fet_endchar;
enddef;



save stafflinethickness;
save increment;

linethickness := 0.5pt#;
increment := 0.5pt#;
y := 10pt#;



for i := 0 step 1 until font_count:

  %% We can't store more than 64 (65?) height dimensions in a TFM
  %% file, so we make small files.

  for j := 0 step 1 until 63:
    % message "l: "&decimal l;
    % note: define_pixels (x) multiplies x by hppp,
    % must never get bigger than infinity
    y := y + increment;

    if y > infinity/hppp:
      message "Resolution and/or magnification is too high";
      error please report: <bug-lilypond@gnu.org>;
    fi

    % x should be about one staff space, taking brace to have
    % default height of 3 staffs, this yields height / 3 / 4 = 12
    % but 15 looks better
    x := y / 15;

    increment := x / 10;
    linethickness :=  min (0.5pt#, y/150);
    if i = font_count:
      draw_brace (y, x, linethickness);
    fi
  endfor;
endfor
  
fet_endfont("feta-braces");
