fet_begingroup("foobars")


def draw_fermata =
	save radius, crook_thinness, crook_fatness, dot_diam;
	
	radius# = 1.5 interline#;
	crook_thinness# = stafflinethickness#;
	crook_fatness# = 4 stafflinethickness#;

	radius# + crook_fatness#/2 = h#;
	radius# + crook_thinness#/2 = w#;
	set_char_box(w#, w#, 0, h#);
	
	define_pixels(radius, crook_thinness, crook_fatness);
	dot_diam = 4/3 crook_fatness;


	penpos1(crook_thinness, 0);
	penpos2(crook_fatness, -90);
	penpos3(crook_thinness, -180);
	z1 = (-radius,0);
	z2 = (0, radius);
	z3 = (radius,0);
	
	fill z1l{down} .. {up}z1r .. z2r .. z3r{down} .. {up}z3l
		.. z2l .. cycle;

	pickup pencircle scaled dot_diam;
	x4 =0;
	bot y4 = - crook_thinness/2;
	draw z4;
enddef;

fet_beginchar("fermata up", "ufermata", "ufermata")
	draw_fermata;	
	penlabels(1,2,3);
	fet_endchar;

fet_beginchar("fermata down", "dfermata", "dfermata")
	draw_fermata;
	currentpicture:=currentpicture yscaled -1 ;
	set_char_box(w#, w#, h#, 0);
	
	fet_endchar;


% FIXME: rounded endings
%
fet_beginchar("> accent", "accent", "accent")
	set_char_box(.9 interline#, .9 interline#, .5 interline#, .5 interline#);
	save thickness, diminish;

	thickness = 2 stafflinethickness;

	% prevent blobs at crossing lines
	diminish = .75;

	y1 + thickness/2 = h;
	x1 = -b;
	x2 = w;
	y2 = .25 thickness* diminish;

	z4 = (w,0);
	x3 =0;
	z3 = whatever [z1, z4];

	penpos2(thickness*(2 - diminish)/2 , 90);
	penpos1(thickness, 90);
	penpos3(thickness, 90);

	penstroke z1e .. z3e .. z2e;
	penstroke (z1e .. z3e .. z2e) yscaled -1;

	penlabels(1,2,3);
	fet_endchar;

fet_beginchar("staccato dot", "staccato", "staccato")
	save radius;
	radius# = 1.25 stafflinethickness#;
	define_pixels(radius);
	pickup pencircle scaled 2 radius;
	draw (0,0);
	set_char_box(radius#, radius#, radius#, radius#);
fet_endchar;

def draw_staccatissimo =
	save radius, height;
	height# = .8 interline#;
	radius# = 2 stafflinethickness#;
	define_pixels(radius, height);

	brush((0,0), blot_diameter, (0, height),2 radius);
	set_char_box(radius#,radius#, blot_diameter#/2, height# + radius#);
enddef;

fet_beginchar("staccatissimo/martellato up", "ustaccatissimo", "ustaccatissimo")
	draw_staccatissimo;
fet_endchar;

%
% FIXEM: scale labels too.
%
fet_beginchar("staccatissimo/martellato down", "dstaccatissimo", "dstaccatissimo")
	draw_staccatissimo;
	y_mirror_char;
fet_endchar;

fet_beginchar("portato/single tenuto", "tenuto", "tenuto")
	save thick;
	thick# = 1.2 stafflinethickness#;
	define_pixels(thick);

	set_char_box(.9 interline#, .9 interline#, thick#/2,thick#/2);
	pickup pencircle scaled thick;
	lft x1 = -b;
	rt x2 = w;
	y1 = y2 = 0;
	draw z1 .. z2;
fet_endchar;


def draw_marcato = 
	save fatness, thinness;
	set_char_box(interline#/2, interline#/2, 0, 1.1 interline#);

	fatness = 3 stafflinethickness;
	thinness = .8 stafflinethickness;
	x2 - x1  + blot_diameter = fatness;
	x2 + thinness/2 = w;
	
	y1 = y2;
	y1 = thinness/2;
	z2 - z3 = whatever * (charwd, -charht);
	z1 - z4 = whatever * (charwd, -charht);
	z3 - z4 = whatever * (charwd, charht);
	y3 + thinness/2 = h;
	draw_rounded_path(z1 -- z2 -- z3 -- z4 -- cycle, thinness);

	pickup pencircle scaled thinness;
	z3 - z5 = whatever* (z3 - z4);
	bot y5 = 0;
	draw z3 .. z5;

	labels(1,2,3,4,5);
enddef;

fet_beginchar("marcato up", "umarcato", "umarcato")
	draw_marcato;
fet_endchar;
fet_beginchar("marcato down", "dmarcato", "dmarcato")
	draw_marcato;
	y_mirror_char;
fet_endchar;


fet_endgroup("foobars");
