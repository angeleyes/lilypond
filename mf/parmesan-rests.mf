% -*-Fundamental-*-
% parmesan-rests.mf -- implement ancient rests
% 
% source file of LilyPond's pretty-but-neat music font
% 
% (c) 2001--2002 Juergen Reuter <reuter@ipd.uka.de>
% 

fet_begingroup("rests")


%%%%%%%%
%
%
%
% MENSURAL NOTATION
%
%
%

pen_width# = 0.3 staff_space#;
pen_height# = 0;
pen_rotation = 30;
slight_pen_rotation = 15;

mens_block_rest_y# = 1/1 staff_space#;
mens_half_block_rest_y# = 5/8 staff_space#;

define_pixels(mens_block_rest_y, mens_half_block_rest_y,
	      pen_width, pen_height);

fet_beginchar("Mensural maxima rest", "-3mensural", "mensmaximarest");
	set_char_box(0.7pen_width#, 0.7pen_width#,
		     mens_block_rest_y#, 2mens_block_rest_y#);
	pickup pencircle
		xscaled pen_width
		yscaled pen_height
		rotated slight_pen_rotation;
	draw (0, -mens_block_rest_y) -- (0, 2 mens_block_rest_y);
fet_endchar;

fet_beginchar("Mensural longa rest", "-2mensural", "menslongarest");
	set_char_box(0.7pen_width#, 0.7pen_width#,
		     mens_block_rest_y#, mens_block_rest_y#);
	pickup pencircle
		xscaled pen_width
		yscaled pen_height
		rotated slight_pen_rotation;
	draw (0, -mens_block_rest_y) -- (0, mens_block_rest_y);
fet_endchar;

fet_beginchar("Mensural breve rest", "-1mensural", "mensbreverest");
	set_char_box(0.7pen_width#, 0.7pen_width#,
		     0, mens_block_rest_y#);
	pickup pencircle
		xscaled pen_width
		yscaled pen_height
		rotated slight_pen_rotation;
	draw (0, 0) -- (0, mens_block_rest_y);
fet_endchar;

fet_beginchar("Mensural whole rest", "0mensural", "menssemibrevisrest");
	set_char_box(0.7pen_width#, 0.7pen_width#,
		     mens_half_block_rest_y#, 0);
	pickup pencircle
		xscaled pen_width
		yscaled pen_height
		rotated slight_pen_rotation;
	draw (0, 0) -- (0, -mens_half_block_rest_y);
fet_endchar;

fet_beginchar("Mensural half rest", "1mensural", "mensminimahalfrest");
	set_char_box(0.7pen_width#, 0.7pen_width#,
		     0, mens_half_block_rest_y#);
	pickup pencircle
		xscaled pen_width
		yscaled pen_height
		rotated slight_pen_rotation;
	draw (0, 0) -- (0, mens_half_block_rest_y);
fet_endchar;

mensrestsize# = .8staff_space#;

fet_beginchar("Mensural 4th rest","2mensural","menssemiminimarest")
	set_char_box(0,mensrestsize#,0,mensrestsize#);
	pickup pencircle
		xscaled pen_width
		yscaled pen_height
		rotated pen_rotation;
	lft x1 =      0;     y1 = 0;
	lft x2 =      0; top y2 = 5/8 h;
	rt  x3 =  4/8 w; bot y3 = 6/8 h;
	rt  x4 = 10/8 w; bot y4 = 4/8 h;
	draw z1 -- z2 -- z3 -- z4;
fet_endchar

fet_beginchar("Mensural 8th rest","3mensural","mensfusarest")
	set_char_box(0,mensrestsize#,0,mensrestsize#);
	pickup pencircle
		xscaled pen_width
		yscaled pen_height
		rotated pen_rotation;
	rt  x1 = 10/8 w;     y1 = 0;
	rt  x2 = 10/8 w; top y2 = 5/8 h;
	lft x3 =  2/8 w; bot y3 = 6/8 h;
	lft x4 =      0; bot y4 = 4/8 h;
	draw z1 -- z2 -- z3 -- z4;
fet_endchar

fet_beginchar("Mensural 16th rest","4mensural","menssemifusarest")
	set_char_box(0,mensrestsize#,0,staff_space#);
	pickup pencircle
		xscaled pen_width
		yscaled pen_height
		rotated pen_rotation;

	rt  x1 = 10/8 w;     y1 = 0;
	rt  x2 = 10/8 w; top y2 = 5.5/8 h;
	lft x3 =  2/8 w; bot y3 = 6.5/8 h;
	lft x4 =      0; bot y4 = 4.5/8 h;
	rt  x5 = 10/8 w; top y5 = 2.5/8 h;
	lft x6 =  2/8 w; bot y6 = 3.5/8 h;
	lft x7 =      0; bot y7 = 1.5/8 h;
	draw z1 -- z2 -- z3 -- z4;
	draw z5 -- z6 -- z7;
fet_endchar

fet_endgroup ("rests")
