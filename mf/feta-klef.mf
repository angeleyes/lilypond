% 
% feta-klef.mf --  implement Clefs
% 
% source file of the Feta (Font-En-Tja) music font
% 
% (c) 1997 Han-Wen Nienhuys <hanwen@stack.nl>
% 

fet_begingroup("klef");


def draw_staff(expr first, last)=
	pickup pencircle scaled stafflinethickness;
	for i:= first step 1 until last:
		draw (- interline, i* interline) .. (4 interline, i* interline);
	endfor

 enddef;



%
% Inspired by Baerenreiter and Breitkopf
% 
% FIXME: dims
% FIXME: right vertical tangent seems to be lower than the F-line
% FIXME: bulb curve smoothly into "long curve" on the  inside
%
%
% [Wanske] says that the extreme x point should be exactly between 
% the dots, but her picture shows that the extreme is ~ 0.2 ss lower

def draw_bass_klef(expr exact_center, reduction) = 
	save reduced_il, left_tilt, left_thick;
	reduced_il# = interline# * reduction;
	left_tilt = 5;
	define_pixels(reduced_il);
	left_thick = .25  reduced_il;
	
	set_char_box(
		- xpart exact_center+ .2 reduced_il#, 
		xpart exact_center + 2.7 reduced_il#, 
		- ypart exact_center + 2.5 reduced_il#, 
		ypart exact_center +reduced_il#);
	x1r - x1l = left_thick;
	z1l = (hround_pixels(xpart exact_center), 
		vround_pixels(ypart exact_center));

	y2 = reduced_il;

	x3l - x1l =  2.1 reduced_il;
	x2 = .5 [x1,x3];
	x3l - x3r = .48 reduced_il;
	y3l = -0.05 interline;
	x4 = x1l - stafflinethickness;
	y4 = -2.2  reduced_il;
	z5 = (x3l +  1/3 reduced_il, .5 reduced_il);

	penpos1(whatever, left_tilt);
	penpos2(1.2 stafflinethickness, -90);
	penpos3(whatever,  185);
	penpos4(stafflinethickness, 135);

	draw_bulb(1, z1r,  z1l, .45 reduced_il, 1.0);


	fill z1r{up} .. z2r{right} .. tension 1.0 .. z3r{down}  .. {curl 0} 
		simple_serif(z4r, z4l, 90) {curl 0}
		.. z3l{up} .. tension 0.9 .. z2l{left} 
		.. z1l{dir (-90 + left_tilt)} -- cycle;
	labels(2,4);
	penlabels(1,2,3,4);

	pickup pencircle scaled (.35 reduced_il);
	draw z5;
	draw z5 yscaled -1;
enddef;



fet_beginchar("F clef ", "bass", "bassclef")
	if test = 1:
		draw_staff(-3,1);
	fi;
	draw_bass_klef((.5 interline#, 0), 1.0);
fet_endchar;
fet_beginchar("F clef (reduced)", "bass_change", "cbassclef")
	draw_bass_klef((.4 interline#, 0),0.8);
fet_endchar;



%
% Inspired by Baerenreiter
% 
% FIXME top should be thinner at z8,
% FIXME bulb should curve (see bass clef)
% FIXME start (inside) should be little thinner
% FIXME parametrise.
% FIXME should be a bit more upright
%

% Beste lezers, kijk,
%
% Een bolletje  hebben we bij toeval allemaal wel eens getekend, maar begint u
% toch eenvoudig.   Eerst een eenvoudig kruis of herstellingsteken
% en via de dubbelslag naar een voorzichtig vlaggetje, en heb geduld!
% Ikzelf heb bijvoorbeeld over mijn eerste gave G-sleutel
% 35 kilobyte metafont, 12 patchlevels, 0 vriendinnen en 45 dagen gedaan
%
def draw_gclef (expr exact_center, reduction)=
	save reduced_il, downstroke_dir, downstroke_angle, hair, center;
	save breapth_factor, inner_thick_end, thinness, thickness, thinnib;
	save inner_start_angle, thinness, thinpen;
	reduced_il# = interline# * reduction;
	define_pixels(reduced_il);
	pair downstroke_dir, center;

	center := (hround_pixels(xpart exact_center), 
		vround_pixels(ypart exact_center));

	hair =  .3 stafflinethickness;
	thinness = 1.3 stafflinethickness;
	downstroke_dir = (14, -75);
	breapth_factor = 11/7;
	inner_thick_end = 45;
	inner_start_angle = angle(-1,-2);
	thickness = .4 reduced_il - hair;

	thinnib = thinness - hair;
	thinpen = thinness;
	set_char_box(-xpart exact_center + breapth_factor* reduced_il#, 
		xpart exact_center + .66 breapth_factor* reduced_il#,
		-ypart exact_center + 3 * reduced_il#,
		ypart exact_center + 5 * reduced_il#);
	
	pickup pencircle scaled hair;

	downstroke_angle = angle downstroke_dir;
	z1 = center + whatever * dir inner_start_angle;
	x1 = xpart center -.28 reduced_il;
	
	top z2r = center + (0,reduced_il + stafflinethickness/2);
	
	x4 = xpart center - .1 reduced_il;
	bot y4r = -(reduced_il + .5 stafflinethickness);

	z3 = (z4 - center) rotated inner_thick_end + center;
	
	z5r = (- breapth_factor, .37)* reduced_il + center;
	penpos5(thickness, 135);

	z6 = center + whatever * downstroke_dir;
	y6 = ypart center + 2 reduced_il;

	z7l - z6 = whatever *(z5- z6) ;
	y7l = 3.5 reduced_il;
	z8r = .4 [z9r, z7r] + 1.5 stafflinethickness * dir 52;

	x9 = .7 [x10, x7r];
	top y9l = 5 reduced_il;

	y10 = 3. reduced_il;
	y11 = -11/7 reduced_il;

	y12 = ypart center - 18.5/7 reduced_il;
	x12 = x11 - 5 /7 reduced_il;	

	z13 = z12 + .6 reduced_il*(-1,1);

	(z10r - z10l) dotprod (unitvector downstroke_dir rotated 90) = 
		thinnib;

	center - z10= whatever * downstroke_dir;
	center - z11 =  whatever * downstroke_dir;

	penpos1(thinnib, inner_start_angle);
	penpos2(thickness, 90);
	penpos3(thinnib, -90 + inner_thick_end);
	penpos4(thinnib, -90);


	penpos7(thickness, 135);
	penpos8(1.5 thinnib, - 70 + angle downstroke_dir);
	penpos9(1.5 thickness, -80);
	penpos10(whatever, downstroke_angle + 10);
	penpos11(thinnib, downstroke_angle + 90);
	penpos12(thinnib, -90);
	penpos13(3 thinnib, 180);

	
	filldraw z2l{right}   .. z3l.. z4l{left} .. z5l{up}  .. z7l{up} 
		%.. z8l	
		.. z9l & z9l ..
		{downstroke_dir}z10l --- z11l -- z11r --- z10r{- downstroke_dir} 
		.. tension .8 
		.. z9r & z9r  
		%.. z8r
		.. z7r{down} .. z5r{down} .. z4r{right}
		.. z3r .. z2r{left} .. 
		tension .95 .. 
		z1r -- z1l 
		.. tension 0.85 ..cycle;

	filldraw simple_serif(z1r, z1l, 90)  -- cycle;

	filldraw z12r{left} .. z13r{up} -- z13l{down} .. z12l{right} .. cycle;

	draw_bulb(-1,  z13l, lft z13r, 6/14 reduced_il, 1.0);

	pickup pencircle scaled (thinpen);
	draw z10 --- z11 .. z12{left};

	penlabels(range 1 thru 15);
enddef;


fet_beginchar("G clef", "violin", "violinclef")
	if test = 1:
		draw_staff(-1,3);
	fi;
	draw_gclef((1.6 interline#,0), 1.0);
fet_endchar;
fet_beginchar("G clef", "violin_change", "cviolinclef")
	draw_gclef((1.3 interline#,0), .8);
fet_endchar;

fet_endgroup("klef");
